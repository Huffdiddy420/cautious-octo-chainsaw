plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'checkstyle'
    id 'org.jetbrains.dokka'
    id 'org.jetbrains.kotlin.plugin.parcelize'
    id 'io.gitlab.arturbosch.detekt' version '1.17.0-RC2'

    id 'signing'
    id 'maven-publish'
}

assemble.dependsOn('lint')
check.dependsOn('checkstyle')

configurations {
    javadocDeps
}

dependencies {
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidLifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidLifecycleVersion"
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.activity:activity-ktx:1.2.3"
    implementation 'com.google.android.gms:play-services-wallet:18.1.2'
    implementation "com.google.android.material:material:$materialVersion"

    // For instructions on replacing the BouncyCastle dependency used by the 3DS2 SDK, see
    // https://github.com/stripe/stripe-android/issues/3173#issuecomment-785176608
    implementation "com.stripe:stripe-3ds2-android:5.3.1"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    javadocDeps 'androidx.annotation:annotation:1.2.0'
    javadocDeps 'androidx.appcompat:appcompat:1.2.0'
    javadocDeps "com.google.android.material:material:$materialVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:3.9.0"
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation "androidx.test:core:$androidTestVersion"
    testImplementation 'org.json:json:20210307'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlinCoroutinesVersion"
    testImplementation 'com.google.truth:truth:1.1.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
    androidTestImplementation "androidx.test:runner:$androidTestVersion"
    androidTestUtil "androidx.test:orchestrator:$androidTestVersion"

    dokkaHtmlPlugin("org.jetbrains.dokka:dokka-base:$dokkaVersion")
    detektPlugins("io.gitlab.arturbosch.detekt:detekt-formatting:1.17.0-RC2")
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.compileSdkVersion
        consumerProguardFiles 'proguard-rules.txt'

        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // From https://developer.android.com/training/testing/junit-runner:
        // > To remove all shared state from your device's CPU and memory after each test,
        // > use the clearPackageData flag.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        main.kotlin.srcDirs += 'src/main/java'
        main.java.srcDirs += 'src/main/java'

        test {

        }
    }
    testOptions {
        unitTests {
            // Note: without this, all Robolectric tests using BuildConfig will fail.
            includeAndroidResources = true
        }
    }
    productFlavors {
    }

    lintOptions {
        enable "Interoperability"
        lintConfig file("../settings/lint.xml")
    }

    dokkaHtml {
        outputDirectory = new File("${project.rootDir}/docs/")
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

check.dependsOn('detekt')

detekt {
    buildUponDefaultConfig = true
    allRules = true
    config = files("$projectDir/../config/detekt.yml")

    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    autoCorrect = true
}

apply from: 'deploy.gradle'